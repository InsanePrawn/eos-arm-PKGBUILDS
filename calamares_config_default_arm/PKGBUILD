# Maintainer : Sravan Pannala <sradjoker@endeavouros.com>
# Contributor : EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_default_arm
pkgver=22.06.1.9
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares ARM configuration files and branding'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('4af5431698f30ae46e0c9c4a2b05c3b0650a09fead13123e034222d3de90d9dfcb11ddd7cb000750b463f4b77b0ffeb5d747ca73d4132258c599c4c390b63fcb')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/endeavouros/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/${_repo_name}/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/${_repo_name}/calamares/settings_community.conf"
    rm "$srcdir/${_repo_name}/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/${_repo_name}/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
