# Maintainer : Sravan Pannala <sradjoker@endeavouros.com>
# Contributor : EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_default_arm
pkgver=22.09.2.3
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares ARM configuration files and branding'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('e6743a2863a9d12e530c0adb583ef690ba95a90eb868ffba6abc5ec33c4fd605960e9c5d0bb10de0cd53c1dd9a15f69223f42572fd0cb071e7706f744240cb3c')


_date=$(date +%Y.%m.%d)

prepare() {
    # mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/$_repo_name-$pkgver/calamares/branding/endeavouros/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/$_repo_name-$pkgver/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/$_repo_name-$pkgver/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/$_repo_name-$pkgver/calamares/settings_community.conf"
    rm "$srcdir/$_repo_name-$pkgver/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/$_repo_name-$pkgver/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/$_repo_name-$pkgver/calamares" "$pkgdir/etc/"
}
